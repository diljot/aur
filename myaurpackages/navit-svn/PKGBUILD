# Contributor:  Gergely Imreh <imrehg(at)gmail(dot)com>
pkgname=navit-svn
pkgver=2565
pkgrel=1
pkgdesc="Bleeding-edge (SVN) version of the modular, touch screen friendly car navigation system with GPS tracking, realtime routing engine and support for various vector map formats."
arch=('i686' 'x86_64')
url="http://wiki.navit-project.org/index.php/Main_Page"
license=('GPL')
provides=('navit')
depends=('qt' 'dbus-glib' 'cairo' 'imlib2' 'sdl_image' 'gtk2' 'gpsd' 'postgresql-libs')
makedepends=('subversion' 'cvs' 'gcc' 'libtool' 'automake' 'autoconf' 'gettext' 'glib2' 'libxt')
optdepends=('cegui>=0.5.0, quesoglc: OpenGL gui')
source=()
md5sums=()
options=(!libtool)
install=navit-svn.install
_svntrunk=https://navit.svn.sourceforge.net/svnroot/navit/trunk/navit
_svnmod=navit

build() {
  cd ${srcdir}

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver 
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  # Create copy for build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  # Finally, build!
  ./autogen.sh || return 1
  ./configure --prefix=/usr --disable-samplemap || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # Remove build directory
  rm -rf ${srcdir}/$_svnmod-build
}
