# Contributor: Sebastian Sareyko <public@nooms.de>
# Maintainer: Gergely Imreh <imrehgATgmailDOTcom>
pkgname=tvbrowser
pkgver=2.7.5
pkgrel=1
pkgdesc="A java-based TV guide which is easily extensible using plugins"
arch=('i686' 'x86_64')
url="http://www.tvbrowser.org"
license=('GPL' 'LGPL' 'BSD' 'CCPL:by-sa' 'APACHE')
depends=('java-runtime' 'x-server')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        http://www.tvbrowser.org/images/$pkgname/${pkgname}_logo.svg
        $pkgname.sh
        $pkgname.desktop)
build() {
  # Removing delivered desktop entry and shell script.
  rm $srcdir/$pkgname-$pkgver/$pkgname.{sh,desktop}

  install -d -m755 $pkgdir/usr/share/java
  cp -R $srcdir/$pkgname-$pkgver $pkgdir/usr/share/java/$pkgname

  # Installing the official scalable logo, which can/will be used by modern window managers / desktop environments that support SVG.
  install -D -m644 $srcdir/${pkgname}_logo.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg

  install -D -m755 $srcdir/$pkgname.sh $pkgdir/usr/bin/$pkgname
  install -D -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

  # Installing the complete bundle of copyright notices. At least because of the BSD license part this step is necessary (http://wiki.archlinux.org/index.php/Arch_Packaging_Standards#Licenses)
  install -D -m644 $srcdir/$pkgname-$pkgver/COPYRIGHT.txt $pkgdir/usr/share/licenses/$pkgname/COPYRIGHT.txt

  # For window managers that don't support SVG (yet) (like e.g. fluxbox), we can also install the set of icons as delivered by the tvbrowser package.
  for _i in 16 32 48 128; do
    install -D -m644 $srcdir/$pkgname-$pkgver/imgs/${pkgname}${_i}.png $pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/$pkgname.png
  done
}
md5sums=('3b7ad683e2ff7d967b85713a917439b6'
         'ac75767a87d94e51afe0897f1614d256'
         '96ae258b2917c54b167102f6bbdc58b4'
         '622e89ee1e3915e42dfa913ea7d98e20')
