# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

pkgname=aerofs
pkgver=EA13.2
pkgrel=1
pkgdesc="Synchronize your files without servers and build private cloud storage out of your own devices"
arch=('i686' 'x86_64')
license=('GPL')
depends=('java-runtime>=6' 'fuse')
url="https://www.aerofs.com"

if [[ $CARCH = 'i686' ]] ; then
  source=("aerofs-$pkgver-$CARCH.tar.gz::https://www.aerofs.com/downloads/aerofs-x86.tar.gz")
  md5sums=('f6a46b114aee4e84013bc40cf73768be')
else
  source=("aerofs-$pkgver-$CARCH.tar.gz::https://www.aerofs.com/downloads/aerofs-$CARCH.tar.gz")
  md5sums=('14e5bff33a6499b84851888f776c6abb')
fi

package() {
  _destdir="opt/$pkgname"
  _docdir="usr/share/doc/$pkgname"
  _desktopdir="usr/share/applications"

  # Install everything to /opt
  cd $srcdir/$pkgname
  mkdir -p $pkgdir/$_destdir/
  cp -r * $pkgdir/$_destdir

  # Create binary links
  mkdir -p $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs-cli $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs-gui $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs-sh $pkgdir/usr/bin/

  # Docs into correct location
  mkdir -p $pkgdir/$_docdir/
  mv "$pkgdir/$_destdir/Getting Started.pdf" $pkgdir/$_docdir

  # Cleanup unrequired files
  rm -f $pkgdir/$_destdir/INSTALL

  # Put the .desktop file in the correct location
  mkdir -p $pkgdir/$_desktopdir/
  mv $pkgdir/$_destdir/aerofs.desktop $pkgdir/$_desktopdir/
}

# vim:set ts=2 sw=2 et:
